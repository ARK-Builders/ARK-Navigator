import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "space.taran.arknavigator"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        testRelease {
            storeFile project.rootProject.file('./testRelease.jks')
            storePassword "navigator"
            keyAlias "key0"
            keyPassword "navigator"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            manifestPlaceholders = [
                appIcon: "@mipmap/ic_launcher_1",
                appLabel: "@string/app_name_debug"
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.testRelease
            manifestPlaceholders = [
                appIcon: "@mipmap/ic_launcher_2",
                appLabel: "@string/app_name"
            ]
        }
    }
}

dependencies {
    implementation files('../libs/arklib.aar')

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    implementation "org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.4"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.1"
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.5.31"

    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
    implementation 'com.github.MikeOrtiz:TouchImageView:3.1.1'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-rc01"

    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "com.github.moxy-community:moxy:2.2.2"
    implementation "com.github.moxy-community:moxy-androidx:2.2.2"
    implementation "com.github.moxy-community:moxy-ktx:2.2.2"
    kapt "com.github.moxy-community:moxy-compiler:2.2.2"

    implementation "androidx.room:room-runtime:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"

    implementation "com.google.dagger:dagger:2.24"
    kapt "com.google.dagger:dagger-compiler:2.24"

    implementation "com.google.android.material:material:1.4.0"

    implementation 'com.beust:klaxon:5.5'

    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    implementation "ru.terrakok.cicerone:cicerone:5.0.0"

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "com.github.barteksc:pdfium-android:1.9.0"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}

task installGitHook(type: Copy) {
    def suffix = "linux"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        suffix = "windows"
    }
    from new File(rootProject.rootDir, "app/app-scripts/pre-commit-$suffix")
    into { new File(rootProject.rootDir, '.git/hooks') }
    rename("pre-commit-$suffix", 'pre-commit')
    fileMode 0775
}

tasks.preBuild.dependsOn ktlintCheck
tasks.preBuild.dependsOn installGitHook
